buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.5"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply from: 'gradle/code-quality.gradle'
apply from: 'gradle/mavenPublish.gradle'

group = 'org.javafxports'
version = '2.0.21'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.publish = !project.version.endsWith('SNAPSHOT')
if (!hasProperty('bintrayUser') || !hasProperty('bintrayKey')) {
    ext.bintrayUser = ''
    ext.bintrayKey = ''
}
compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases/'
    }
    mavenCentral()
    jcenter()
    google()
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 1, 'minutes'
    resolutionStrategy.cacheChangingModulesFor 1, 'minutes'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:3.0.1'
    compile 'com.gluonhq:gvmbuilder:0.0.27'
    compile 'javax.activation:javax.activation-api:1.2.0'
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    compile 'com.sun.xml.bind:jaxb-core:2.3.0'
    compile 'com.sun.xml.bind:jaxb-impl:2.3.0'
}

configurations {
    published
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

// Allow mixed groovy/java source builds
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourceJar, groovydocJar
}

bintray {
    user = bintrayUser
    key = bintrayKey

    configurations = [ 'published', 'archives' ]
    dryRun = !project.publish
    publish = project.publish
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'The jfxmobile-plugin is a gradle plugin that unifies the building of JavaFX applications for three different target platforms: android, ios and desktop.'
        websiteUrl = 'http://javafxports.org'
        issueTrackerUrl = 'https://github.com/javafxports/javafxmobile-plugin/issues'
        vcsUrl = 'https://github.com/javafxports/javafxmobile-plugin'
        licenses = [ 'BSD 3-Clause' ]
        labels = [ 'ios', 'android', 'mobile', 'javafx', 'desktop', 'gradle', 'plugin' ]
        version {
            name = project.version
            vcsTag = project.version
            attributes = [ 'gradle-plugin' : "org.javafxports.jfxmobile:${project.group}:${project.name}" ]
        }
    }
}

